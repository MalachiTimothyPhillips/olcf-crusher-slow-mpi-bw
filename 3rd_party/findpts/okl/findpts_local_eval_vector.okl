@kernel void findpts_local_eval_vector(
              const dlong outputOffset,
              const dlong fieldOffset,
              dfloat * const out_base,   const dlong out_stride,
        const dlong  * const el_base,    const dlong el_stride,
        const dfloat * const r_base,     const dlong r_stride,
        const dlong  pn,
        @restrict const dfloat * const in)
{
  for(dlong i=0;i<pn;++i;@outer){

    @shared dfloat wtr[p_Nq];
    @shared dfloat wts[p_Nq];
    @shared dfloat wtt[p_Nq];
    @shared dfloat sums[3][p_Nq];

    const dfloat * r = CAT(dfloat, r, i);

    for(dlong fld = 0; fld < 3; ++fld; @inner(1)){
      for(dlong j=0;j<p_Nq;++j;@inner(0)){
        if(fld == 0){
          lagrange_eval(wtr, r[0], j);
          lagrange_eval(wts, r[1], j);
          lagrange_eval(wtt, r[2], j);
        }
      }
    }
    @barrier("local");

    const dlong el = *CAT(dlong, el, i);
    const dfloat *in_el = in + el*p_Np;

    for(dlong fld = 0; fld < 3; ++fld; @inner(1)){
    for (dlong j=0;j<p_Nq;++j;@inner(0)){
      dfloat sum_j = 0;
      for(dlong k=0;k<p_Nq;++k){
        dfloat sum_k = 0;

        for(dlong l=0;l<p_Nq;++l){
          sum_k += in_el[j + k*p_Nq + l*p_Nq*p_Nq + fld * fieldOffset] * wtt[l];
        }
        sum_j += wts[k]*sum_k;
      }
      sums[fld][j] = wtr[j]*sum_j;
    }
    }
    @barrier("local");
#if p_Nq>16
    for(dlong fld = 0; fld < 3; ++fld; @inner(1)){
    for(dlong j=0;j<p_Nq;++j;@inner(0)){
      if(j<16&&j<p_Nq-16){
        sums[fld][j] += sums[fld][j+16];
      }
    }
    }
    @barrier("local");
#endif
#if p_Nq>8
    for(dlong fld = 0; fld < 3; ++fld; @inner(1)){
    for(dlong j=0;j<p_Nq;++j;@inner(0)){
      if(j<8&&j<p_Nq-8){
        sums[fld][j] += sums[fld][j+8];
      }
    }
    }
    @barrier("local");
#endif
#if p_Nq>4
    for(dlong fld = 0; fld < 3; ++fld; @inner(1)){
    for(dlong j=0;j<p_Nq;++j;@inner(0)){
      if(j<4&&j<p_Nq-4){
        sums[fld][j] += sums[fld][j+4];
      }
    }
    }
    @barrier("local");
#endif
#if p_Nq>2
    for(dlong fld = 0; fld < 3; ++fld; @inner(1)){
    for(dlong j=0;j<p_Nq;++j;@inner(0)){
      if(j<2&&j<p_Nq-2){
        sums[fld][j] += sums[fld][j+2];
      }
    }
    }
    @barrier("local");
#endif
    for(dlong fld = 0; fld < 3; ++fld; @inner(1)){
    for(dlong j=0;j<p_Nq;++j;@inner(0)){
      if(j==0){
        dfloat * result = AT(dfloat, out, i) + fld * outputOffset;
        *result = sums[fld][0] + sums[fld][1];
      }
    }
    }
    @barrier("local");
  }
}
