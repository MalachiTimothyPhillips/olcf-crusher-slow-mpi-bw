@kernel void enforceUnHex3D(const dlong Nelements,
                                         const dlong offset,
                                         @restrict const dlong*  elementList,
                                         @restrict const dfloat*  normals,
                                         @restrict const dfloat*  tangentials1,
                                         @restrict const dfloat*  tangentials2,
                                         @restrict const dlong*  vmapM,
                                         @restrict const int*  EToB,
                                         @restrict const int*  BToBCType,
                                         @restrict outputType*  U)
{
  for(dlong element = 0; element < Nelements; element++; @outer(0)) {

    @exclusive dlong e;

    for(int f = 0; f < p_Nfaces; f++) {
      for(int m = 0; m < p_Nfp; ++m; @inner(0)) {
        e = elementList[element];
        const int n = m + f * p_Nfp;
        const int sk = e * p_Nfp * p_Nfaces + n;
        const dlong sid  = e * p_Nfaces * p_Nfp + n;
        const dlong idM = vmapM[sk];
        const dlong bid = EToB[f + p_Nfaces * e];

        const dlong bcType = bid >= 0 ? BToBCType[bid] : -1;

        if(bcType == p_ZERO_NORMAL) {

          const dfloat t1x = tangentials1[idM + 0 * Nelements * p_Np];
          const dfloat t1y = tangentials1[idM + 1 * Nelements * p_Np];
          const dfloat t1z = tangentials1[idM + 2 * Nelements * p_Np];

          const dfloat t2x = tangentials2[idM + 0 * Nelements * p_Np];
          const dfloat t2y = tangentials2[idM + 1 * Nelements * p_Np];
          const dfloat t2z = tangentials2[idM + 2 * Nelements * p_Np];


          const dfloat u = U[idM + 0 * offset];
          const dfloat v = U[idM + 1 * offset];
          const dfloat w = U[idM + 2 * offset];

          const dfloat Rtn1 = (u * t1x + v * t1y + w * t1z);
          const dfloat Rtn2 = (u * t2x + v * t2y + w * t2z);

          U[idM + 0 * offset] = outputType(Rtn1 * t1x + Rtn2 * t2x);
          U[idM + 1 * offset] = outputType(Rtn1 * t1y + Rtn2 * t2y);
          U[idM + 2 * offset] = outputType(Rtn1 * t1z + Rtn2 * t2z);
        }
      }
      @barrier("global");
    }

  }
}
