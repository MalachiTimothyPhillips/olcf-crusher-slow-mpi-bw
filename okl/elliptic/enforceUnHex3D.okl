@kernel void enforceUnHex3D(const dlong Nelements,
                                         const dlong offset,
                                         @restrict const dlong*  elementList,
                                         @restrict const dfloat*  sgeo,
                                         @restrict const dlong*  vmapM,
                                         @restrict const int*  EToB,
                                         @restrict const int*  BToBCType,
                                         @restrict outputType*  U)
{
  for(dlong element = 0; element < Nelements; element++; @outer(0)) {

    @exclusive dlong e;

    for(int f = 0; f < p_Nfaces; f++) {
      for(int m = 0; m < p_Nfp; ++m; @inner(0)) {
        e = elementList[element];
        const int n = m + f * p_Nfp;
        const int sk = e * p_Nfp * p_Nfaces + n;
        const dlong sid  = e * p_Nfaces * p_Nfp + n;
        const dlong idM = vmapM[sk];
        const dlong bid = EToB[f + p_Nfaces * e];

        const dlong bcType = bid >= 0 ? BToBCType[bid] : -1;

        if(bcType == p_ZERO_NORMAL) {

          const dfloat nx = sgeo[sid * p_Nsgeo + p_NXID];
          const dfloat ny = sgeo[sid * p_Nsgeo + p_NYID];
          const dfloat nz = sgeo[sid * p_Nsgeo + p_NZID];

          const dfloat t1x = sgeo[sid * p_Nsgeo + p_T1XID];
          const dfloat t1y = sgeo[sid * p_Nsgeo + p_T1YID];
          const dfloat t1z = sgeo[sid * p_Nsgeo + p_T1ZID];

          const dfloat t2x = sgeo[sid * p_Nsgeo + p_T2XID];
          const dfloat t2y = sgeo[sid * p_Nsgeo + p_T2YID];
          const dfloat t2z = sgeo[sid * p_Nsgeo + p_T2ZID];


          const dfloat u = U[idM + 0 * offset];
          const dfloat v = U[idM + 1 * offset];
          const dfloat w = U[idM + 2 * offset];

          const dfloat Rtn1 = (u * t1x + v * t1y + w * t1z);
          const dfloat Rtn2 = (u * t2x + v * t2y + w * t2z);

          const outputType results_x = outputType(Rtn1 * t1x + Rtn2 * t2x);
          const outputType results_y = outputType(Rtn1 * t1y + Rtn2 * t2y);
          const outputType results_z = outputType(Rtn1 * t1z + Rtn2 * t2z);

          // check U\dot n condition
          const dfloat dot = results_x * nx + results_y * ny + results_z * nz;
          if(sqrt(dot*dot) > 1e-8){
            printf("enforceUnHex3D: UdotN = 0 violated, dot = %g, bid = %d!\n", dot, bid);
          }

          U[idM + 0 * offset] = results_x;
          U[idM + 1 * offset] = results_y;
          U[idM + 2 * offset] = results_z;


        }
      }
      @barrier("global");
    }

  }
}
