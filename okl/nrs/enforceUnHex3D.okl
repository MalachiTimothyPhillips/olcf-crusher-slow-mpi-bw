/*

   The MIT License (MIT)

   Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in all
   copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
   SOFTWARE.

 */
//RHS contributions for continuous solver

@kernel void enforceUnHex3D(const dlong Nelements,
                                         const dlong offset,
                                         @restrict const dlong*  elementList,
                                         @restrict const dfloat*  sgeo,
                                         @restrict const dfloat*  Vn,
                                         @restrict const dfloat*  V1,
                                         @restrict const dfloat*  V2,
                                         @restrict const dfloat*  Vmask,
                                         @restrict const dlong*  vmapM,
                                         @restrict const int*  EToB,
                                         @restrict dfloat*  U)
{
  for(dlong element = 0; element < Nelements; element++; @outer(0)) {

    @exclusive dlong e;

    for(int f = 0; f < p_Nfaces; f++) {
      for(int m = 0; m < p_Nfp; ++m; @inner(0)) {
        e = elementList[element];
        const int n = m + f * p_Nfp;
        const int sk = e * p_Nfp * p_Nfaces + n;
        const dlong sid  = e * p_Nfaces * p_Nfp + n;
        const dlong idM = vmapM[sk];
        const dlong bcType = EToB[f + p_Nfaces * e];

        if(bcType > 0) {
          if(bcType == 7) {

            const dfloat vnx = sgeo[sid * p_Nsgeo + p_NXID];
            const dfloat vny = sgeo[sid * p_Nsgeo + p_NYID];
            const dfloat vnz = sgeo[sid * p_Nsgeo + p_NZID];

            const dfloat v1x = V1[idM + 0 * offset];
            const dfloat v1y = V1[idM + 1 * offset];
            const dfloat v1z = V1[idM + 2 * offset];

            const dfloat v2x = V2[idM + 0 * offset];
            const dfloat v2y = V2[idM + 1 * offset];
            const dfloat v2z = V2[idM + 2 * offset];

            const dfloat u = U[idM + 0 * offset];
            const dfloat v = U[idM + 1 * offset];
            const dfloat w = U[idM + 2 * offset];

            const dfloat r1mask = Vmask[idM + 0 * offset];
            const dfloat r2mask = Vmask[idM + 1 * offset];
            const dfloat r3mask = Vmask[idM + 2 * offset];

            const dfloat Rnor = r1mask * (u * vnx + v * vny + w * vnz);
            const dfloat Rtn1 = r2mask * (u * v1x + v * v1y + w * v1z);
            const dfloat Rtn2 = r3mask * (u * v2x + v * v2y + w * v2z);

            U[idM + 0 * offset] = Rnor * vnx + Rtn1 * v1x + Rtn2 * v2x;
            U[idM + 1 * offset] = Rnor * vny + Rtn1 * v1y + Rtn2 * v2y;
            U[idM + 2 * offset] = Rnor * vnz + Rtn1 * v1z + Rtn2 * v2z;
          }
        }
      }
      @barrier("global");
    }

  }
}
